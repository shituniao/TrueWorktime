Version :="v1.1.0"
bannerWidth :=90
ItemWidth :=60
FileEncoding "UTF-8"

;ä¸»é¢˜é¢œè‰²map
Theme := Map()
Theme["red"]:="f92f60" ;çº¢f92f60/ffd8d9é»„ffc700/7d4533è“1c5cd7/aeddffç»¿008463/c6fbe7
Theme["redT"]:="ffd8d9"
Theme["yellow"]:="ffc700"
Theme["yellowT"]:="7d4533"
Theme["blue"]:="1c5cd7"
Theme["blueT"]:="aeddff"
Theme["green"]:="008463"
Theme["greenT"]:="c6fbe7"
Theme["black"]:="000000"
Theme["blackT"]:="ffffff"
Theme["white"]:="ffffff"
Theme["whiteT"]:="000000"
Theme["gray"]:="919191"
Theme["grayT"]:="1f1f1f"

;å¼•å…¥å¤–éƒ¨JSONåº“ï¼Œæ¥è‡ªhttps://github.com/G33kDude/cJson.ahk
#Include JSON.ahk 

;--------å…³äºJSONè¯»å–å’Œå†™å…¥çš„æµ‹è¯•
;objstr := [{start:"sss",time:1234,color:"ff0000"},{start:"bbb",time:1234,color:"ff0000"}]
;OutputDebug objstr[1].start
;jsonstr :=JSON.Dump(objstr)
;FileAppend jsonstr,"test.json"
ItemJson:=FileRead("Itemdata.json")
;OutputDebug jsona
Items:=JSON.Load(ItemJson)
;Items[1]['time']+=1
;OutputDebug jsonb
;--------å…³äºJSONè¯»å–å’Œå†™å…¥çš„æµ‹è¯•

logger := StateLog() ;å®šä¹‰è®¡æ—¶å™¨å¯¹è±¡
TraySetIcon(, , 1) ;å†»ç»“æ‰˜ç›˜å›¾æ ‡

;è¯»å–iniæ–‡ä»¶
WorkExe:=StrSplit(IniRead("Config.ini","exelist","workexe"),",") ;å·¥ä½œè½¯ä»¶åˆ—è¡¨
SitLimit:=1800 ; ä¹…åæ—¶é—´
;WorkExe:=["HarmonyPremium.exe", "PureRef.exe", "tim.exe"] 

;è®¡æ—¶å™¨æ‚¬æµ®çª—
ClockGui := Gui()
ClockGui.Opt("+AlwaysOnTop -Caption +ToolWindow +DPIScale" ) ; +ToolWindow é¿å…æ˜¾ç¤ºä»»åŠ¡æ æŒ‰é’®å’Œ alt-tab èœå•é¡¹.
ClockGui.MarginY:=4
ClockGui.BackColor := Theme["white"] ; åˆå§‹ç™½è‰²èƒŒæ™¯(ä¸‹é¢ä¼šå˜æˆåŠé€æ˜çš„).
ClockGui.SetFont("s12","Microsoft YaHei UI") 
if(WorkExe.Length>0){
    ClockText := ClockGui.Add("Text", "x0 ym r1 w" bannerWidth " c" Theme["whiteT"] " Center", "é¢„å¤‡") 
}else{
    ClockGui.BackColor := Theme["black"]
    ClockText := ClockGui.Add("Text", "x0 ym r1 w" bannerWidth " c" Theme["blackT"] " Center", "æ— å·¥ä½œè½¯ä»¶") 
}
WinSetTransColor(" 230", ClockGui) ; åŠé€æ˜:
WinSetExStyle("+0x20", ClockGui) ;é¼ æ ‡ç©¿é€
ClockGui.Show("x" logger.x "y" logger.y " h30 w" bannerWidth " NoActivate") ; NoActivate è®©å½“å‰æ´»åŠ¨çª—å£ç»§ç»­ä¿æŒæ´»åŠ¨çŠ¶æ€.

;ç´¯è®¡è®¡æ—¶å™¨æ‚¬æµ®çª—
ItemGui := Gui()
ItemGui.Opt("+AlwaysOnTop -Caption +ToolWindow +DPIScale" )
ItemGui.MarginY:=4
ItemGui.BackColor := Items[logger.CurrentItem]['Theme'] ;çº¢f92f60/ffd8d9é»„ffc700/7d4533è“1c5cd7/aeddffç»¿008463/c6fbe7
ItemGui.SetFont("s12","Microsoft YaHei UI") 
ItemText :=ItemGui.Add("Text","x0 ym r1 w" ItemWidth " c" Items[logger.CurrentItem]['ThemeT'] " Center", FormatSeconds(Items[1]['time'],False))
WinSetTransColor(" 230", ItemGui) ; åŠé€æ˜:
WinSetExStyle("+0x20", ItemGui) ;é¼ æ ‡ç©¿é€
ItemGui.Show("x" logger.x-ItemWidth "y" logger.y " h30 w" ItemWidth " NoActivate")

;è®¡æ—¶å™¨è®¾ç½®çª—å£
Config :=Gui()
Config.Title :="å·¥ä½œè®¡æ—¶å™¨"
Config.MarginX :=12
Config.MarginY :=15
Config.SetFont("s10","Microsoft YaHei UI")
Config.AddText("y+10","ç›®å‰è®¾ç½®çš„å·¥ä½œè½¯ä»¶ï¼š")
WorkList :=Config.AddEdit("y+10 w300 R9 vWorkList ReadOnly Backgrounddddddd Border",)
Config.AddText("y+15","æ·»åŠ æ–°çš„å·¥ä½œè½¯ä»¶ï¼š")
ExeWork :=Config.AddListView("y+10 xm r9 vExeWork w300 -Hdr -Multi",["åç§°"])
ExeWorkIcon := IL_Create()
ExeWork.SetImageList(ExeWorkIcon)
ExeWork.ModifyCol(1, 250)
ExeWork.OnEvent("ItemSelect",ExeWork_ItemSelect)
Config.Add("Button", "y+10 w80", "â•æ·»åŠ ").OnEvent("Click", ClickADD)
Config.Add("Button", "x+30 w80", "â”åˆ·æ–°").OnEvent("Click", ClickREFRESH)
Config.Add("Button", "x+30 w80", "âŒæ¸…ç©º").OnEvent("Click", ClickCLEAR)
;Config.Add("Button", "xm w80 w300", "âœ”ï¸æäº¤").OnEvent("Click", ClickSUBMIT)
Config.AddText("y+10 xm w300","é€‰æ‹©ä½ è®¤ä¸ºæ˜¯å·¥ä½œç”¨çš„è½¯ä»¶ï¼Œç‚¹å‡»æ·»åŠ æŒ‰é’®").SetFont("s10 c000000")
Config.AddText("y+2 xm w300","å¦‚æœåˆ—è¡¨ä¸­æ²¡æœ‰è¦é€‰çš„è½¯ä»¶ï¼Œå°è¯•å…ˆæ‰“å¼€è¿™ä¸ªè½¯ä»¶ï¼Œç„¶åç‚¹å‡»åˆ·æ–°æŒ‰é’®ï¼Œç¨‹åºä¼šè‡ªåŠ¨æ£€æµ‹").SetFont("s9 c444444")
Caution :=Config.AddText("y+10 vCaution w300")
Caution.SetFont("s9 c444444")
SelectItem:=["",1]

;å¸®åŠ©çª—å£
Help:=Gui()
Help.Title:="å·¥ä½œè®¡æ—¶å™¨ " Version
Help.MarginX :=12
Help.MarginY :=15
Help.SetFont("s9 c444444","Microsoft YaHei UI")

logger.Start ;âœ…âœ…âœ…âœ…âœ…âœ…--------------------å¯åŠ¨è®¡æ—¶å™¨-----------------------

;å®šä¹‰æ‰˜ç›˜å›¾æ ‡
A_TrayMenu.Rename("E&xit","é€€å‡º")
A_TrayMenu.Delete("&Suspend Hotkeys")
A_TrayMenu.Delete("&Pause Script")
A_TrayMenu.Insert("1&", "æš‚åœ", MenuHandler)
A_TrayMenu.Insert("2&", "é‡ç½®", MenuHandler)
A_TrayMenu.Insert("3&")
A_TrayMenu.Insert("4&", "ä¹…å30åˆ†é’Ÿæé†’", MenuHandler)
if(logger.tomatoToggle){
    A_TrayMenu.check("4&")
}else{
    A_TrayMenu.UnCheck("4&")
}
A_TrayMenu.Insert("5&", "æ˜¾ç¤ºé¡¶éƒ¨æµ®çª—", MenuHandler)
if(IniRead("Config.ini","setting","show","1")="1"){
    A_TrayMenu.check("5&")
}else{
    A_TrayMenu.UnCheck("5&")
}
MonitorMenu :=Menu()
A_TrayMenu.Insert("6&", "é¡¶éƒ¨æµ®çª—æ˜¾ç¤ºåœ¨...", MonitorMenu)
Loop MonitorGetCount(){
    MonitorMenu.Add("æ˜¾ç¤ºå™¨" A_Index , MonitorChoose)
}
A_TrayMenu.Insert("7&", "è®¾ç½®å·¥ä½œè½¯ä»¶", MenuHandler)
A_TrayMenu.Insert("8&")
A_TrayMenu.Insert("9&", "å¸®åŠ©", MenuHandler)
A_TrayMenu.Insert("10&", "æ˜¾ç¤ºç»Ÿè®¡å›¾", MenuHandler)

A_TrayMenu.Default:="10&"

;Persistent
;æ‰˜ç›˜æ§ä»¶åŠŸèƒ½åŠç¨‹åºè®¾ç½®ç•Œé¢
MenuHandler(ItemName, ItemPos, MyMenu) {
    Switch ItemPos{
    Case 4 :
        {
            logger.sitTime:=0
            if(logger.tomatoToggle){
                logger.tomatoToggle:=0
                A_TrayMenu.Uncheck("4&")
            }else{
                logger.tomatoToggle:=1
                A_TrayMenu.Check("4&")
            }
            IniWrite logger.tomatoToggle,"Config.ini","setting","tomato_alarm"
        }
    Case 1:
        {
            Pause -1
            if(A_IsPaused){
                A_IconTip := "è®¡æ—¶å™¨å·²æš‚åœ`nå·¥ä½œæ—¶é—´ï¼š" FormatSeconds(logger.WorkTime) "`næ‘¸é±¼æ—¶é—´ï¼š" FormatSeconds(logger.BreakTime)
                A_TrayMenu.Rename("1&","ç»§ç»­")
                TrayTip , "è®¡æ—¶å™¨å·²æš‚åœ"
                Sleep 2000 ; è®©å®ƒæ˜¾ç¤º 3 ç§’é’Ÿ.
                TrayTip
            }else{
                A_TrayMenu.Rename("1&","æš‚åœ")
                TrayTip , "è®¡æ—¶å™¨å·²ç»§ç»­"
                Sleep 2000 ; è®©å®ƒæ˜¾ç¤º 3 ç§’é’Ÿ.
                TrayTip
            }
        }

    Case 2 :
        {
            logger.WorkTime :=0
            logger.BreakTime :=0
            ClockText.Value := FormatSeconds(logger.WorkTime)
            TrayTip , "è®¡æ—¶å™¨å·²é‡ç½®"
            Sleep 2000 ; è®©å®ƒæ˜¾ç¤º 3 ç§’é’Ÿ.
            TrayTip
        }
    Case 5:
        {
            if(IniRead("Config.ini","setting","show","1")="1"){
                A_TrayMenu.uncheck("5&")
                ClockGui.Hide()
                IniWrite "0","Config.ini","setting","show"
            }else{
                A_TrayMenu.Check("5&")
                ClockGui.Show()
                IniWrite "1","Config.ini","setting","show"
            }
        }
    Case 7 :
        {
            ShowConfig()
        }
    Case 9:
        {
            Help.Show("AutoSize Center")
        }
    Case 10:
        {
            Run "ShowWorkTime", "ShowWorkTime\"
        }
    }
}
;---------------------ç”¨åˆ°çš„å„ç§æ‰˜ç›˜åŠŸèƒ½å‡½æ•°ğŸ‘‡--------------------------------------
MonitorChoose(ItemName, ItemPos, MyMenu){
    MonitorGet ItemPos, &WL, &WT, &WR, &WB
    logger.x := WR/(A_ScreenDPI/96)-(bannerWidth + 137)
    ;MsgBox(logger.x)
    logger.y := WT/(A_ScreenDPI/96)
    ClockGui.Move(logger.x,logger.y)
    IniWrite ItemPos,"Config.ini","setting","monitor"
}

ShowConfig(){
    Config.Show("AutoSize Center")
    ExeWork.Focus()
    WorkCACHE:=""
    for n in WorkExe{
        WorkCACHE .=StrSplit(StrTitle(n),".exe")[1] "`n"
    }
    WorkList.Value:=WorkCACHE
    ExeWork.Delete()
    ids := WinGetList() ;è·å–å½“å‰ç¨‹åºåˆ—è¡¨
    ;ExeNameList :=[]
    ENL_p :=[] ;ç¨‹åºåˆ—è¡¨å»é‡
    hased :=0
    for this_id in ids
    {
        for this_n in ENL_p{
            if (WinGetProcessName(this_id) =this_n){
                hased:=1
                Break
            }
        }
        if (hased =0){
            ;ExeNameList.Push(StrSplit(WinGetProcessName(this_id),".exe")[1])
            ExeWork.Add("Icon" IL_Add(ExeWorkIcon, WinGetProcessPath(this_id)) ,StrSplit(StrTitle(WinGetProcessName(this_id)),".exe")[1])
            ENL_p.Push(StrTitle(WinGetProcessName(this_id)))
            ;OutputDebug WinGetProcessPath(this_id)  ;æµ‹è¯•ç¨‹åºçš„è¿›ç¨‹åœ°å€ï¼ˆæ¶‰åŠåˆ°è·å–ç¨‹åºIconå›¾æ ‡
        }
        hased :=0
    }
    Return 
}
;---------------------------ç”¨åˆ°çš„å„ç§æ‰˜ç›˜åŠŸèƒ½å‡½æ•°ğŸ‘†----------------------------------
;---------------------------è½¯ä»¶è®¾ç½®çª—å£çš„åŠŸèƒ½å‡½æ•°ğŸ‘‡----------------------------------
ClickADD(thisGui, *)
{
    hased :=0
    ;Choosed := thisGui.Gui.Submit(0).SelectItem
    if(SelectItem[1]!=""){
        for n in WorkExe{
            if ((SelectItem[1] ".exe") =n){
                hased :=1
                Break
            }
        }
        if(hased =0){
            WorkExe.Push(SelectItem[1] ".exe")
            WorkList.Value.=SelectItem[1] "`n"
            Caution.Value := "æ·»åŠ æˆåŠŸï¼"
        }else{
            Caution.Value := "è¿™ä¸ªè½¯ä»¶å·²ç»æ·»åŠ è¿‡äº†ï¼"
        }
        iniCache:="workexe="
        for n in WorkExe{
            iniCache .=n ","
        }
        ;å†™å…¥iniæ–‡ä»¶
        iniCache := RTrim(iniCache,",")
        IniWrite iniCache,"Config.ini","exelist"
    }else{
        Caution.Value := "ä½ é€‰äº†å•¥ï¼Ÿ"
    }
}

ClickREFRESH(thisGui, *){
    ids := WinGetList() ;è·å–å½“å‰ç¨‹åºåˆ—è¡¨
    hased :=0
    for this_id in ids
    {
        Loop ExeWork.GetCount()
        {
            if(ExeWork.GetText(A_Index)=StrSplit(StrTitle(WinGetProcessName(this_id)),".exe")[1]){
                hased:=1
                Break
            }
        }
        if (hased =0){
            ;ExeNameList.Push(StrSplit(WinGetProcessName(this_id),".exe")[1])
            ExeWork.Add("Icon" IL_Add(ExeWorkIcon, WinGetProcessPath(this_id)) ,StrSplit(StrTitle(WinGetProcessName(this_id)),".exe")[1])
        }
        hased :=0
    }
    Caution.Value := "è½¯ä»¶åˆ—è¡¨åˆ·æ–°å®Œæˆï¼"
}

ClickCLEAR(thisGui, *){
    if(WorkExe.Length >0){
        WorkExe.RemoveAt(1, WorkExe.Length)
        iniCache:="workexe="
        for n in WorkExe{
            iniCache .=n ","
        }
        ;å†™å…¥iniæ–‡ä»¶
        iniCache := RTrim(iniCache,",")
        IniWrite iniCache,"Config.ini","exelist"
        WorkList.Value:=""
        Caution.Value := "å·¥ä½œè½¯ä»¶å·²æ¸…ç©ºï¼"
    }else{
        Caution.Value := "å·²ç»æ˜¯ç©ºçš„äº†å•Šï¼"
    }

}

ExeWork_ItemSelect(EW, Item, Selected){
    if(Selected){
        SelectItem[1]:=EW.GetText(Item)
        SelectItem[2]:=Item
        ;MsgBox(SelectItem[1] " " SelectItem[2])
    }
}

;å¯åŠ¨æ—¶æ£€æµ‹æ˜¯å¦ç¨‹åºåˆ—è¡¨ä¸ºç©º
if(WorkExe.Length<=0){
    ;TrayTip "å³é”®ç‚¹å‡»ä»»åŠ¡æ å›¾æ ‡è¿›è¡Œè®¾ç½®", "å°šæœªè®¾ç½®å·¥ä½œè½¯ä»¶"
    ;Sleep 5000 ; è®©å®ƒæ˜¾ç¤º 3 ç§’é’Ÿ.
    ;TrayTip
    if(MsgBox("å°šæœªè®¾ç½®å·¥ä½œè½¯ä»¶ï¼Œæ˜¯å¦è¿›è¡Œè®¾ç½®ï¼Ÿ","å·¥ä½œè®¡æ—¶å™¨","4 64")="Yes"){
        ShowConfig()
    }
}
;---------------------------è½¯ä»¶è®¾ç½®çª—å£çš„åŠŸèƒ½å‡½æ•°ğŸ‘†----------------------------------

;è®¡æ—¶å™¨ç±»ï¼ˆæ ¸å¿ƒç¨‹åº
class StateLog {
    __New(){
        MonitorGet IniRead("Config.ini","setting","monitor"), &WL, &WT, &WR, &WB
        this.x:=WR - (bannerWidth + 137)*(A_ScreenDPI/96)
        this.y:=WT
        this.WorkTime :=0 ;å·¥ä½œæ—¶é—´
        this.BreakTime :=0 ;æ‘¸é±¼æ—¶é—´
        this.LeaveTime :=0 ;ç¦»å¼€æ—¶é—´
        this.CurrentItem :=IniRead("Config.ini","data","current_item") ;å½“å‰é¡¹ç›®
        this.StartTime :=FormatTime(,"yyyy-MM-dd HH:mm:ss") ;æœ¬æ¬¡è®¡æ—¶å¼€å§‹è¿è¡Œæ—¶é—´
        this.RunTime :=0 ;æ€»è¿è¡Œæ—¶é—´
        this.WorkIn :=2 ;è®¡æ—¶å™¨çŠ¶æ€ï¼Œ1-å·¥ä½œä¸­ï¼Œ2-æ‘¸é±¼ä¸­ï¼Œ3-ç¦»å¼€ä¸­ï¼Œ 0-æœªè®¾ç½®å·¥ä½œè½¯ä»¶   ,4-ä¹…åæé†’
        this.sitTime :=0
        this.tomatoToggle:=IniRead("Config.ini","setting","tomato_alarm")
        this.check :=ObjBindMethod(this, "StateCheck")
        ;OutputDebug CCCTTT
        ;this.tmtAlarm :=ObjBindMethod(this, "TomatoAlarm")
    }
    Start() {
        ;æš‚æ—¶å°†æ­¤è¡Œæ³¨é‡Šï¼Œå› ä¸ºä¸æ‰“ç®—ç”¨cherrysodaäº†
        ;FileAppend "START," (A_Hour*3600+A_Min*60+A_Sec) "," FormatTime(, "HH:mm:ss"), this.StartTime ".csv"
        SetTimer this.check, 1000
    }
    StateCheck() {
        this.RunTime++
        if(WorkExe.Length<=0){ ;0-æœªè®¾ç½®å·¥ä½œè½¯ä»¶
            WorkIn:=4
            if(this.WorkIn !=0){
                this.WorkIn:=0
                ClockGui.BackColor := Theme["black"]
                ClockText.SetFont("c" Theme["blackT"])
                ;æš‚æ—¶å°†æ­¤è¡Œæ³¨é‡Šï¼Œå› ä¸ºä¸æ‰“ç®—ç”¨cherrysodaäº†
                ;FileAppend "`n" "NONE," this.RunTime "," FormatTime(, "HH:mm:ss"), this.StartTime ".csv"
            }
            ClockText.Value := "æ— å·¥ä½œè½¯ä»¶"
            ChangeItem(0) ;æ‚¬æµ®çª—å˜æˆç°è‰²
        }else{
            if(ifwinAct() and A_TimeIdlePhysical<30000){ ;1-å·¥ä½œä¸­
                this.WorkTime++
                this.sitTime++
                Items[this.CurrentItem]['time']++
                if (this.WorkIn != 1){ 
                    if(this.WorkIn != 4){
                        ;æš‚æ—¶å°†æ­¤è¡Œæ³¨é‡Šï¼Œå› ä¸ºä¸æ‰“ç®—ç”¨cherrysodaäº†
                        ;FileAppend "`n" "work," this.RunTime "," FormatTime(, "HH:mm:ss"), this.StartTime ".csv"
                    }
                    this.WorkIn :=1 ;è®¡æ—¶å™¨çŠ¶æ€ï¼Œ1-å·¥ä½œä¸­ï¼Œ2-æ‘¸é±¼ä¸­ï¼Œ3-ç¦»å¼€ä¸­ï¼Œ 0-æœªè®¾ç½®å·¥ä½œè½¯ä»¶   ,4-ä¹…åæé†’
                    ClockGui.BackColor := Theme["black"]
                    ClockText.SetFont("c" Theme["blackT"])
                }
                ClockText.Value := FormatSeconds(this.WorkTime)
                ItemText.Value := FormatSeconds(Items[this.CurrentItem]['time'],False)
                ChangeItem(this.CurrentItem)
                JsonFileReUpdate()
            }else if(A_TimeIdlePhysical>=30000){ ;3-ç¦»å¼€ä¸­
                this.LeaveTime++
                this.BreakTime++ ;æ”¹åŠ¨ï¼šç¦»å¼€åæ—¶é—´ä¹Ÿè®¡å…¥æ‘¸é±¼æ—¶é—´
                this.sitTime:=0
                if (this.WorkIn != 3){
                    ;æš‚æ—¶å°†æ­¤è¡Œæ³¨é‡Šï¼Œå› ä¸ºä¸æ‰“ç®—ç”¨cherrysodaäº†
                    ;FileAppend "`n" "leave," this.RunTime "," FormatTime(, "HH:mm:ss"), this.StartTime ".csv"
                    this.WorkIn :=3 
                    ClockGui.BackColor := Theme["white"]
                    ClockText.SetFont("c" Theme["whiteT"])

                }
                ClockText.Value := FormatSeconds(this.BreakTime) ;æ”¹åŠ¨ï¼šç¦»å¼€åæ—¶é—´ä¹Ÿè®¡å…¥æ‘¸é±¼æ—¶é—´
                ChangeItem(0) ;æ‚¬æµ®çª—å˜æˆç°è‰²
            }else{ ;2-æ‘¸é±¼ä¸­
                this.BreakTime++
                this.sitTime++
                if (this.WorkIn != 2){
                    if(this.WorkIn != 4){
                        ;æš‚æ—¶å°†æ­¤è¡Œæ³¨é‡Šï¼Œå› ä¸ºä¸æ‰“ç®—ç”¨cherrysodaäº†
                        ;FileAppend "`n" "break," this.RunTime "," FormatTime(, "HH:mm:ss"), this.StartTime ".csv"
                    }
                    this.WorkIn :=2 ;è®¡æ—¶å™¨çŠ¶æ€ï¼Œ1-å·¥ä½œä¸­ï¼Œ2-æ‘¸é±¼ä¸­ï¼Œ3-ç¦»å¼€ä¸­ï¼Œ 0-æœªè®¾ç½®å·¥ä½œè½¯ä»¶   ,4-ä¹…åæé†’
                    ClockGui.BackColor := Theme["white"]
                    ClockText.SetFont("c" Theme["whiteT"])

                }
                ClockText.Value := FormatSeconds(this.BreakTime)
                ChangeItem(0) ;æ‚¬æµ®çª—å˜æˆç°è‰²
            }
        }
        if(this.tomatoToggle=1 and Mod(this.sitTime,SitLimit)>0 and Mod(this.sitTime,SitLimit)<3 and this.sitTime>SitLimit){ ;4-ä¹…åæé†’
            if (this.WorkIn != 4){
                this.WorkIn :=4 ;è®¡æ—¶å™¨çŠ¶æ€ï¼Œ1-å·¥ä½œä¸­ï¼Œ2-æ‘¸é±¼ä¸­ï¼Œ3-ç¦»å¼€ä¸­ï¼Œ 0-æœªè®¾ç½®å·¥ä½œè½¯ä»¶   ,4-ä¹…åæé†’
                ClockGui.BackColor := "ea4135"
                ClockText.SetFont("cffffff")
                ClockText.Value := "åå¤ªä¹…äº†"
            }
        }
        ; æ‰˜ç›˜å›¾æ ‡æç¤º
        Switch this.WorkIn{
        Case 1:
            A_IconTip := "å·¥ä½œä¸­...`nå·¥ä½œæ—¶é—´ï¼š" FormatSeconds(this.WorkTime) "`næ‘¸é±¼æ—¶é—´ï¼š" FormatSeconds(this.BreakTime) "`nç¦»å¼€æ—¶é—´ï¼š" FormatSeconds(this.LeaveTime)
        Case 2:
            A_IconTip := "æ‘¸é±¼ä¸­...`nå·¥ä½œæ—¶é—´ï¼š" FormatSeconds(this.WorkTime) "`næ‘¸é±¼æ—¶é—´ï¼š" FormatSeconds(this.BreakTime) "`nç¦»å¼€æ—¶é—´ï¼š" FormatSeconds(this.LeaveTime)
        Case 0:
            A_IconTip := "å°šæœªè®¾ç½®å·¥ä½œè½¯ä»¶`nå³é”®å›¾æ ‡é€‰æ‹©è®¾ç½®" 
        }
    }
}

ifwinAct() ;æ ¸å¿ƒå‡½æ•°ï¼åˆ¤æ–­å½“å‰è½¯ä»¶æ˜¯å¦ä¸ºå·¥ä½œè½¯ä»¶
{
    for app in WorkExe{
        if(WinActive("ahk_exe " app)){
            ;MsgBox(WorkExe.Length)
        Return 1
    }
}
Return 0
}

FormatSeconds(NumberOfSeconds,full := True) ; æŠŠæŒ‡å®šçš„ç§’æ•°è½¬æ¢æˆ hh:mm:ss æ ¼å¼.
{
    ;é‡å†™äº†æ—¶é—´æ ¼å¼åŒ–
    HH:=Floor(NumberOfSeconds/3600)
    mm:=Floor(Mod(NumberOfSeconds,3600)/60)
    ss:=Mod(NumberOfSeconds,60)
    if(full){
        Return Format("{1:02u}:{2:02u}:{3:02u}" , HH,mm,ss) 
    }else{ 
        Return Format("{1:02u}:{2:02u}" , HH,mm) 
    }
}

JsonFileReUpdate(){
    FileDelete "Itemdata.json"
    FileAppend JSON.Dump(Items),"Itemdata.json"
}

; å¸®åŠ©æ–‡æœ¬
Help.AddText("y+10 w300","è¯´æ˜ï¼š").SetFont("s10 bold")
Help.AddText("xp+10 y+10 w280","å·¥ä½œè®¡æ—¶å™¨æ˜¯ä¸€ä¸ªå¸®åŠ©ç”¨æˆ·è®°å½•å·¥ä½œæ—¶é•¿å’Œç©ºé—²æ—¶é•¿çš„ç¨‹åºã€‚")
Help.AddText("y+10 w280","ç¨‹åºæ¯ç§’æ£€æµ‹å½“å‰æ­£åœ¨ä½¿ç”¨çš„è½¯ä»¶æ˜¯å¦æ˜¯é¢„å…ˆè®¾å®šçš„å·¥ä½œè½¯ä»¶ï¼Œä»¥åŠç”¨æˆ·æ˜¯å¦åœ¨30ç§’å†…æœ‰é¼ æ ‡æ“ä½œæˆ–é”®ç›˜è¾“å…¥ã€‚")
Help.AddText("xm y+10 w300","å›¾ä¾‹ï¼š").SetFont("s10 bold")
Help.AddText("xp+10 y+10 w280","è‹¥å½“å‰è½¯ä»¶æ˜¯å·¥ä½œè½¯ä»¶ï¼Œä¸”ç”µè„‘åœ¨30ç§’å†…æœ‰é”®é¼ æ“ä½œï¼Œåˆ™ä¼šè®°å½•ä¸ºå·¥ä½œæ—¶é—´ï¼Œæ˜¾ç¤ºä¸ºé»‘åº•ç™½å­—ã€‚")
Help.AddText("y+10 BackGround000000 cffffff h8 w" bannerWidth " Center","")
Help.AddText("y+0 BackGround000000 cffffff h30 w" bannerWidth " Center","06:29:01").SetFont("s12")
Help.AddText("y+10 w280","è‹¥å½“å‰è½¯ä»¶ä¸æ˜¯å·¥ä½œè½¯ä»¶ï¼Œåˆ™ä¼šè®°å½•ä¸ºç©ºé—²æ—¶é—´ï¼Œæ˜¾ç¤ºä¸ºç™½åº•é»‘å­—ã€‚")
Help.AddText("y+10 BackGroundffffff c000000 h8 w" bannerWidth " Center","")
Help.AddText("y+0 BackGroundffffff c000000 h30 w" bannerWidth " Center","05:13:22").SetFont("s12")
Help.AddText("y+10 w280","è‹¥è¶…è¿‡30ç§’æ²¡æœ‰æ“ä½œï¼Œåˆ™ä¼šè®°å½•ä¸ºç¦»å¼€æ—¶é—´ï¼Œæ˜¾ç¤ºä¸ºç°åº•ç™½å­—ã€‚")
Help.AddText("y+10 BackGroundffffff c666666 h8 w" bannerWidth " Center","")
Help.AddText("y+0 BackGroundffffff c666666 h30 w" bannerWidth " Center","05:13:22").SetFont("s12")
Help.AddText("y+10 w280","æä¾›ä¹…åæé†’åŠŸèƒ½ï¼Œå½“ç”¨æˆ·ç»´æŒé”®é¼ æ“ä½œè¶…è¿‡30åˆ†é’Ÿæ—¶ï¼Œç¨‹åºä¼šæ˜¾ç¤ºçº¢è‰²ä¹…åæç¤ºï¼ˆè¿™ä¸ªåŠŸèƒ½å¯ä»¥å…³é—­ï¼‰")
Help.AddText("y+10 BackGroundea4135 cffffff h8 w" bannerWidth " Center","")
Help.AddText("y+0 BackGroundea4135 cffffff h30 w" bannerWidth " Center","åå¤ªä¹…äº†").SetFont("s12")
Help.AddText("xm y+20 w300","ä½œè€…ä¸è”ç³»æ–¹å¼ï¼š").SetFont("s10 bold")
Help.AddText("xp+10 y+10 w280","æœ¬ç¨‹åºåŸºäºAutoHotkey 2.0.2ç¼–å†™`nç”±shituniaoåˆ¶ä½œ`næ—¶é•¿ç»Ÿè®¡å›¾ç¨‹åºéƒ¨åˆ†ç”±C.Evenç¼–å†™")
Help.AddLink("y+10 w280", '<a href="https://www.autohotkey.com/">AutoHotkeyå®˜ç½‘</a>')
Help.AddLink("y+5 w280", '<a href="https://github.com/shituniao/TrueWorktime">Githubåœ°å€</a>')
Help.AddLink("y+5 w280 h0",).Focus()

OnExit ExitFunc

ExitFunc(ExitReason, ExitCode)
{
    ;æš‚æ—¶å°†æ­¤è¡Œæ³¨é‡Šï¼Œå› ä¸ºä¸æ‰“ç®—ç”¨cherrysodaäº†
    ;FileAppend "`n" "END," logger.RunTime "," FormatTime(, "HH:mm:ss") " å·¥ä½œæ—¶é—´:" FormatSeconds(logger.WorkTime) "ï¼›æ‘¸é±¼æ—¶é—´:" FormatSeconds(logger.BreakTime) "ï¼›ç¦»å¼€æ—¶é—´:" FormatSeconds(logger.LeaveTime) "`n",logger.StartTime ".csv"
}

;æ¢ç®—å‘¨å‡ çš„å­—ç¬¦
WeekDay(){
    Switch A_WDay{
        Case "1": Return "_Sunday"
        Case "2": Return "_Monday"
        Case "3": Return "_Tuesday"
        Case "4": Return "_Wednesday"
        Case "5": Return "_Thursday"
        Case "6": Return "_Friday"
        Case "7": Return "_Saturday"
    }

}

;ä¿®æ”¹é¡¹ç›®æ‚¬æµ®çª—é¢œè‰²
ChangeItem(Item){
    if(Item!=0){
        logger.CurrentItem := Item
        ItemGui.BackColor := Items[logger.CurrentItem]['Theme']
        ItemText.SetFont("c" Items[logger.CurrentItem]['ThemeT'])
        ;OutputDebug logger.CurrentItem
        OutputDebug Items[logger.CurrentItem]['time']
    }else{
        ItemGui.BackColor := Theme["gray"]
        ItemText.SetFont("c" Theme["grayT"])
    }

}
^F1::
    {
        ChangeItem(1)
    }
^F2::
    {
        ChangeItem(2)
    }
^F3::
    {
        ChangeItem(3)
    }
^F4::
    {
        ChangeItem(4)
    }